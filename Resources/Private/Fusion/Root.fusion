root {
	accordionPagesFrontend {
		condition = ${q(node).is('[instanceof Dotpulse.AccordionPages:Content]') && node.context.live}
		renderer = Neos.Fusion:Http.Message {
			httpResponseHead {
				statusCode = 301
				headers.Location = Dotpulse.AccordionPages:NodeUri
			}
		}
	}

	accordionPagesBackend {
		condition = ${q(node).is('[instanceof Dotpulse.AccordionPages:Content]')}
		renderer = Page {
			body {
				templatePath = 'resource://Dotpulse.AccordionPages/Private/Templates/TypoScriptObjects/Accordion.html'
				content = ContentCollection {
					nodePath = 'main'
				}
			}
		}
	}
}

prototype(Dotpulse.AccordionPages:NodeUri) < prototype(NodeUri) {
	@context.nodeObject = ${node}
	node = ${q(nodeObject).is('[instanceof Neos.NodeTypes:Page]') ? nodeObject : q(nodeObject).parents('[instanceof Neos.NodeTypes:Page]').get(0)}
	section = Dotpulse.AccordionPages:CollectionProperty
}

prototype(Dotpulse.AccordionPages:CollectionProperty) < prototype(Neos.Fusion:Collection) {
	property = 'uriPathSegment'
	nodeObject = ${nodeObject ? nodeObject : node}

	seperator = Neos.Fusion:RawArray {
		first = ''
		default = '--'
	}

	@context.property = ${this.property}
	@context.seperator = ${this.seperator}

	array = ${q(this.nodeObject).add(q(this.nodeObject).parentsUntil('[instanceof Neos.NodeTypes:Page]')).get()}
	collection = ${Array.reverse(this.array)}
	iterationName = 'iterator'
	itemName = 'element'
	itemRenderer = ${(iterator.isFirst ? seperator.first : seperator.default) + q(element).property(property)}
}

prototype(Dotpulse.AccordionPages:Container) < prototype(Content) {
	attributes {
		class = Neos.Fusion:RawArray {
			accordion = 'accordion'
			backend = 'neos-accordion-backend'
			backend.@if.isInBackend = ${node.context.inBackend}
		}
		data-close-others = ${q(node).property('closeOthers') ? true : false}
		data-open-first = ${q(node).property('firstOpen') ? true : false}
	}

	content = Neos.Fusion:Collection {
		collection = ${q(documentNode).children('[instanceof Dotpulse.AccordionPages:Content][_hiddenInIndex=false]').get()}
		itemName = 'node'
		iterationName = 'pageIterator'
		itemRenderer = Dotpulse.AccordionPages:Content
	}

	@cache {
		mode = 'cached'
		maximumLifetime = 0
		entryIdentifier = Neos.Fusion:RawArray {
			node = ${node}
			editPreviewMode = ${node.context.currentRenderingMode.name}
			uri = ${String.toString(request.httpRequest.uri)}
		}
		entryTags {
			# Whenever the node changes..
			1 = ${'Node_' + documentNode.identifier}
			# Whenever a child node changes..
			2 = ${'DescendantOf_' + documentNode.identifier}
		}
	}
}

prototype(Dotpulse.AccordionPages:Content) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Dotpulse.AccordionPages/Private/Templates/NodeTypes/Content.html'

	attributes = Neos.Fusion:Attributes {
		class = Neos.Fusion:RawArray {
			item = 'accordion-item'
			active = ${q(node).property('active') ? 'accordion-item-active' : ''}
		}
		id = ${q(node).property('uriPathSegment')}
	}
	titleAttributes = Neos.Fusion:Attributes {
		class = 'accordion-title'
	}
	contentAttributes = Neos.Fusion:Attributes {
		class = 'accordion-content'
	}

	content = ContentCollection {
		nodePath = 'main'
		attributes {
			class = 'accordion-content-inner'
			class.@process.collectionClass >
		}
	}
}
